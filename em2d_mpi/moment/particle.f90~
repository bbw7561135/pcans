module particle


  implicit none

  private

  public :: particle__solv


contains


  subroutine particle__solv(up,uf,np,nxgs,nxge,nygs,nyge,nys,nye,nsp,np2,delt,c)

    integer, intent(in)    :: np, nys, nye, nsp
    integer, intent(in)    :: np2(nys:nye,nsp)
    real(8), intent(in)    :: delt, c
    real(8), intent(in)    :: uf(6,nxgs-1:nxge+1,nygs-1:nyge+1)
    real(8), intent(inout) :: up(5,np,nys:nye,nsp)
    integer :: i, j, ii, isp, ih, jh
    real(8) :: dx, dxm, dy, dym
    real(8) :: fac1, fac2, fac2r, fac3, fac3r, gam, txxx, bt2
    real(8) :: pf(6,np,nys:nye)
    real(8) :: uvm(6,np,nys:nye)

    do isp=1,nsp

       !interpolate fields to particles
       do j=nys,nye
          do ii=1,np2(j,isp)
             i  = floor(up(1,ii,j,isp))
             ih = floor(up(1,ii,j,isp)-0.5)
             jh = floor(up(2,ii,j,isp)-0.5)

             !Bx at (i+1/2, j)
             dx = up(1,ii,j,isp)-0.5-ih
             dxm = 1.-dx
             dy = up(2,ii,j,isp)-j
             dym = 1.-dy
             pf(1,ii,j) = +(dxm*uf(1,ih,j  )+dx*uf(1,ih+1,j  ))*dym &
                          +(dxm*uf(1,ih,j+1)+dx*uf(1,ih+1,j+1))*dy

             !By at (i, j+1/2)
             dx = up(1,ii,j,isp)-i
             dxm = 1.-dx
             dy = up(2,ii,j,isp)-0.5-jh
             dym = 1.-dy
             pf(2,ii,j) = +(dxm*uf(2,i,jh  )+dx*uf(2,i+1,jh  ))*dym &
                          +(dxm*uf(2,i,jh+1)+dx*uf(2,i+1,jh+1))*dy

             !Bz at (i, j)
             dy = up(2,ii,j,isp)-j
             dym = 1.-dy
             pf(3,ii,j) = +(dxm*uf(3,i,j  )+dx*uf(3,i+1,j  ))*dym &
                          +(dxm*uf(3,i,j+1)+dx*uf(3,i+1,j+1))*dy

             !Ex at (i, j+1/2)
             dy = up(2,ii,j,isp)-0.5-jh
             dym = 1.-dy
             pf(4,ii,j) = +(dxm*uf(4,i,jh  )+dx*uf(4,i+1,jh  ))*dym &
                          +(dxm*uf(4,i,jh+1)+dx*uf(4,i+1,jh+1))*dy

             !Ey at (i+1/2, j)
             dx = up(1,ii,j,isp)-0.5-ih
             dxm = 1.-dx
             dy = up(2,ii,j,isp)-j
             dym = 1.-dy
             pf(5,ii,j) = +(dxm*uf(5,ih,j  )+dx*uf(5,ih+1,j  ))*dym &
                          +(dxm*uf(5,ih,j+1)+dx*uf(5,ih+1,j+1))*dy

             !Ez at (i+1/2, j+1/2)
             dy = up(2,ii,j,isp)-0.5-jh
             dym = 1.-dy
             pf(6,ii,j) = +(dxm*uf(6,ih,jh  )+dx*uf(6,ih+1,jh  ))*dym &
                          +(dxm*uf(6,ih,jh+1)+dx*uf(6,ih+1,jh+1))*dy
          enddo
       enddo

       fac1 = q(isp)/r(isp)*0.5*delt
       do j=nys,nye
          do ii=1,np2(j,isp)
             uvm(1,ii,j) = up(3,ii,j,isp)+fac1*pf(4,ii,j)
          enddo
       enddo
       do j=nys,nye
          do ii=1,np2(j,isp)
             uvm(2,ii,j) = up(4,ii,j,isp)+fac1*pf(5,ii,j)
          enddo
       enddo
       do j=nys,nye
          do ii=1,np2(j,isp)
             uvm(3,ii,j) = up(5,ii,j,isp)+fac1*pf(6,ii,j)
          enddo
       enddo

       fac2 = fac1/c
       do j=nys,nye
          do ii=1,np2(j,isp)
             gam = dsqrt(1.0+(+uvm(1,ii,j)*uvm(1,ii,j) &
                              +uvm(2,ii,j)*uvm(2,ii,j) &
                              +uvm(3,ii,j)*uvm(3,ii,j))/(c*c))
             fac2r = fac2/gam
             uvm(4,ii,j) = uvm(1,ii,j) &
                  +fac2r*(+uvm(2,ii,j)*pf(3,ii,j)-uvm(3,ii,j)*pf(2,ii,j))
             uvm(5,ii,j) = uvm(2,ii,j) &
                  +fac2r*(+uvm(3,ii,j)*pf(1,ii,j)-uvm(1,ii,j)*pf(3,ii,j))
             uvm(6,ii,j) = uvm(3,ii,j) &
                  +fac2r*(+uvm(1,ii,j)*pf(2,ii,j)-uvm(2,ii,j)*pf(1,ii,j))
          enddo
       enddo
    
       txxx = fac1*fac1
       fac3 = q(isp)*delt/r(isp)
       do j=nys,nye
          do ii=1,np2(j,isp)
             bt2 = pf(1,ii,j)*pf(1,ii,j)+pf(2,ii,j)*pf(2,ii,j)+pf(3,ii,j)*pf(3,ii,j)
             gam = dsqrt(1.0+(+uvm(1,ii,j)*uvm(1,ii,j) &
                              +uvm(2,ii,j)*uvm(2,ii,j) &
                              +uvm(3,ii,j)*uvm(3,ii,j))/(c*c))
             fac3r = fac3/(gam+txxx*bt2/gam)
             uvm(1,ii,j) = uvm(1,ii,j) &
                  +fac3r*(+uvm(5,ii,j)*pf(3,ii,j)-uvm(6,ii,j)*pf(2,ii,j))
             uvm(2,ii,j) = uvm(2,ii,j) &
                  +fac3r*(+uvm(6,ii,j)*pf(1,ii,j)-uvm(4,ii,j)*pf(3,ii,j))
             uvm(3,ii,j) = uvm(3,ii,j) &
                  +fac3r*(+uvm(4,ii,j)*pf(2,ii,j)-uvm(5,ii,j)*pf(1,ii,j))
          enddo
       enddo

       do j=nys,nye
          do ii=1,np2(j,isp)
             up(3,ii,j,isp) = uvm(1,ii,j)+fac1*pf(4,ii,j)
          enddo
       enddo
       do j=nys,nye
          do ii=1,np2(j,isp)
             up(4,ii,j,isp) = uvm(2,ii,j)+fac1*pf(5,ii,j)
          enddo
       enddo
       do j=nys,nye
          do ii=1,np2(j,isp)
             up(5,ii,j,isp) = uvm(3,ii,j)+fac1*pf(6,ii,j)
          enddo
       enddo

    enddo

  end subroutine particle__solv


end module particle
